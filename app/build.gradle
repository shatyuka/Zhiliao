apply plugin: 'com.android.application'

android {
    namespace 'com.shatyuka.zhiliao'
    compileSdk 35
    defaultConfig {
        applicationId "com.shatyuka.zhiliao"
        minSdkVersion 26
        targetSdkVersion 35
        ndkVersion "27.2.12479018"
        versionCode appVerCode.toInteger()
        versionName appVerName
        externalNativeBuild {
            cmake {
                abiFilters "arm64-v8a", "armeabi-v7a"
                arguments "-DANDROID_STL=none"
                cppFlags "-std=c++20", "-Qunused-arguments", "-Wno-gnu-string-literal-operator-template", "-fno-rtti", "-fvisibility=hidden", "-fvisibility-inlines-hidden", "-fno-exceptions", "-fno-stack-protector", "-fomit-frame-pointer", "-Wno-builtin-macro-redefined", "-Wno-unused-value", "-D__FILE__=__FILE_NAME__"
                cFlags "-std=c18", "-Qunused-arguments", "-Wno-gnu-string-literal-operator-template", "-fno-rtti", "-fvisibility=hidden", "-fvisibility-inlines-hidden", "-fno-exceptions", "-fno-stack-protector", "-fomit-frame-pointer", "-Wno-builtin-macro-redefined", "-Wno-unused-value", "-D__FILE__=__FILE_NAME__"
            }
        }
    }
    if (project.hasProperty('releaseStoreFile')) {
        signingConfigs {
            config {
                storeFile = file(project.properties['releaseStoreFile'])
                storePassword = project.properties['releaseStorePassword']
                keyAlias = project.properties['releaseKeyAlias']
                keyPassword = project.properties['releaseKeyPassword']
            }
        }
    }
    applicationVariants.configureEach { variant ->
        variant.resValue "string", "app_version", variant.versionName
        variant.outputs.configureEach { output ->
            if (variant.buildType.name == 'release') {
                outputFileName = "Zhiliao_${defaultConfig.versionName}.apk"
            }
        }
    }
    buildTypes {
        configureEach {
            if (project.hasProperty('releaseStoreFile')) {
                signingConfig signingConfigs.config
            } else {
                println("using debug sign")
                signingConfig signingConfigs.debug
            }
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            externalNativeBuild {
                cmake {
                    cppFlags "-flto", "-ffunction-sections", "-fdata-sections", "-Wl,--gc-sections", "-fno-unwind-tables", "-fno-asynchronous-unwind-tables", "-Wl,--exclude-libs,ALL"
                    cFlags "-flto", "-ffunction-sections", "-fdata-sections", "-Wl,--gc-sections", "-fno-unwind-tables", "-fno-asynchronous-unwind-tables", "-Wl,--exclude-libs,ALL"
                    arguments "-DCMAKE_BUILD_TYPE=Release", "-DCMAKE_CXX_FLAGS_RELEASE=-Oz -DNDEBUG", "-DCMAKE_C_FLAGS_RELEASE=-Oz -DNDEBUG"
                }
            }
        }
        debug {
            externalNativeBuild {
                cmake {
                    arguments "-DCMAKE_CXX_FLAGS_DEBUG=-Og", "-DCMAKE_C_FLAGS_DEBUG=-Og"
                }
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1+"
        }
    }
    buildFeatures {
        prefab true
    }
    packagingOptions {
        jniLibs {
            excludes += ['META-INF/**', 'kotlin/**', 'google/**']
            useLegacyPackaging false
        }
        resources {
            excludes += ['META-INF/**', 'kotlin/**', 'google/**', '**.bin']
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'dev.rikka.ndk.thirdparty:cxx:1.2.0'
    testImplementation 'junit:junit:4.13.2'
    compileOnly 'de.robv.android.xposed:api:82'
    testImplementation 'de.robv.android.xposed:api:82'
}
